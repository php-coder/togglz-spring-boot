buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url "http://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
        classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
    }
}

ext {
    hamcrestVersion       = "1.3"
    javaVersion           = "1.7"
    junitVersion          = "4.12"
    springVersion         = "4.2.4.RELEASE"
    springBootVersion     = "1.3.1.RELEASE"
    togglzVersion         = "2.3.0-SNAPSHOT"
    servletApiVersion     = "3.1.0"
    springVersion         = "4.2.4.RELEASE"
    springBootVersion     = "1.3.1.RELEASE"
    springSecurityVersion = "4.0.3.RELEASE"
}

configure(allprojects) {

    group = "com.github.marceloverdijk"
}

configure(subprojects) {

    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "propdeps"

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    compileJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }
}

project("togglz-spring-boot-starter") {

    description = "Togglz Spring Boot Starter"

    apply plugin: "maven"

    dependencies {
        compile("org.togglz:togglz-core:${togglzVersion}")
        compile("org.togglz:togglz-spring-core:${togglzVersion}")
        optional("org.springframework.security:spring-security-config:${springSecurityVersion}")
        optional("org.springframework.security:spring-security-web:${springSecurityVersion}")
        optional("org.togglz:togglz-console:${togglzVersion}")
        optional("org.togglz:togglz-spring-security:${togglzVersion}")
        provided("javax.servlet:javax.servlet-api:$servletApiVersion")
        provided("org.springframework:spring-web:${springVersion}")
        provided("org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}")
        testCompile("junit:junit:${junitVersion}")
        testCompile("org.hamcrest:hamcrest-all:${hamcrestVersion}")
    }
}

configure(subprojects.findAll { project -> project.name.startsWith("togglz-spring-boot-sample") }) {

    apply plugin: "spring-boot"

    dependencies {
        compile("org.springframework.boot:spring-boot-devtools")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile project(":togglz-spring-boot-starter")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.togglz:togglz-junit:${togglzVersion}")
    }
}

project("togglz-spring-boot-sample-hello-world") {

    description = "Togglz Spring Boot Sample Hellow World"

    dependencies {
        compile("org.togglz:togglz-console:${togglzVersion}")
    }
}

project("togglz-spring-boot-sample-spring-security") {

    description = "Togglz Spring Boot Sample Spring Security"

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-security")
        compile("org.togglz:togglz-console:${togglzVersion}")
        compile("org.togglz:togglz-spring-security:${togglzVersion}")
        testCompile("org.springframework.security:spring-security-test")
    }
}

configure(rootProject) {

    description = "Togglz Spring Boot"

    task wrapper(type: Wrapper) {
        gradleVersion = "2.10"
    }
}
