buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url "http://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
        classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
    }
}

ext {
    javaVersion = "1.7"
    springVersion = "4.2.4.RELEASE"
    springBootVersion = "1.3.1.RELEASE"
    togglzVersion = "2.3.0-SNAPSHOT"
    servletApiVersion = "3.1.0"
}

configure(subprojects) {

    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "propdeps"

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    compileJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }
}

project("togglz-spring-boot-starter") {

    description = "Togglz Spring Boot Starter"

    dependencies {
        compile("org.togglz:togglz-core:${togglzVersion}")
        compile("org.togglz:togglz-console:${togglzVersion}")
        compile("org.togglz:togglz-spring-core:${togglzVersion}")
        provided("javax.servlet:javax.servlet-api:$servletApiVersion")
        provided("org.springframework:spring-web:4.2.4.RELEASE")
        provided("org.springframework.boot:spring-boot-autoconfigure:1.3.1.RELEASE")
    }
}

configure(subprojects.findAll { project -> project.name.startsWith("togglz-spring-boot-sample") }) {

    apply plugin: "spring-boot"

    dependencies {
        // compile("org.springframework.boot:spring-boot-devtools")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile project(":togglz-spring-boot-starter")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.togglz:togglz-junit:${togglzVersion}")
    }
}

configure(rootProject) {

    description = "Togglz Spring Boot"

    task wrapper(type: Wrapper) {
        gradleVersion = "2.10"
    }
}
